buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.23.0'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

def versionMajor = 2
def versionMinor = 4
def versionPatch = 7
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    project.ext {
        LABCOAT_FABRIC_KEY = project.hasProperty("LABCOAT_FABRIC_KEY") ? project.LABCOAT_FABRIC_KEY : ""
        KEYSTORE_PATH = project.hasProperty("KEYSTORE_PATH") ? project.KEYSTORE_PATH : " "
        KEYSTORE_PASSWORD = project.hasProperty("KEYSTORE_PASSWORD") ? project.KEYSTORE_PASSWORD : " "
        KEYSTORE_ALIAS = project.hasProperty("KEYSTORE_ALIAS") ? project.KEYSTORE_ALIAS : " "
        KEY_PASSWORD = project.hasProperty("KEY_PASSWORD") ? project.KEY_PASSWORD : " "
    }

    defaultConfig {
        applicationId "com.commit451.gitlab"
        minSdkVersion 16
        targetSdkVersion 26
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        manifestPlaceholders = [fabric_key: project.ext.LABCOAT_FABRIC_KEY]
        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true
    }

    flavorDimensions "type"

    productFlavors {

        normal {
            dimension "type"
        }

        fdroid {
            dimension "type"
        }
    }

    signingConfigs {
        release {
            storeFile file(project.ext.KEYSTORE_PATH)
            storePassword project.ext.KEYSTORE_PASSWORD
            keyAlias project.ext.KEYSTORE_ALIAS
            keyPassword project.ext.KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.enableCrashlytics = true
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.enableCrashlytics = false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        exclude 'META-INF/MANIFEST.MF'
    }
}

ext {
    supportLibVersion = '26.0.0'
    retrofitVersion = '2.3.0'
    okHttpVersion = '3.8.1'
    butterknifeVersion = '8.7.0'
    parcelerVersion = '1.1.9'
    reptarVersion = '2.5.0'
    adapterLayout = '1.1.1'
    materialDialogsVersion = '0.9.4.5'
    leakCanaryVersion = '1.5'
    addendumVersion = '1.3.1'
    alakazamVersion = '1.0.1'
    moshiVersion = '1.5.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"

    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:support-v13:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:palette-v7:$supportLibVersion"
    implementation "com.android.support:customtabs:$supportLibVersion"
    implementation 'com.android.support:multidex:1.0.2'

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation("com.squareup.retrofit2:converter-simplexml:$retrofitVersion") {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"

    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    kapt "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    implementation 'com.jakewharton.timber:timber:4.5.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    implementation 'org.greenrobot:eventbus:3.0.0'

    implementation 'io.reactivex.rxjava2:rxjava:2.1.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'

    implementation "org.parceler:parceler-api:$parcelerVersion"
    kapt "org.parceler:parceler:$parcelerVersion"

    implementation "com.github.Commit451.Reptar:reptar:$reptarVersion"
    implementation "com.github.Commit451.Reptar:reptar-retrofit:$reptarVersion"
    implementation "com.github.Commit451.Reptar:reptar-kotlin:$reptarVersion"
    implementation 'com.github.Commit451:ElasticDragDismissLayout:1.0.4'
    implementation "com.github.Commit451.AdapterLayout:adapterlayout:$adapterLayout"
    implementation("com.github.Commit451.AdapterLayout:adapterflowlayout:$adapterLayout") {
        exclude group: 'com.wefika', module: 'flowlayout'
    }
    //https://github.com/blazsolar/FlowLayout/issues/31
    implementation("com.wefika:flowlayout:0.4.1") {
        exclude group: 'com.intellij', module: 'annotations'
    }
    implementation 'com.github.Commit451.Easel:easel:2.1.1'
    implementation 'com.github.Commit451:Gimbal:2.0.2'
    implementation 'com.github.Commit451:Teleprinter:1.0.2'
    implementation 'com.github.Commit451:Jounce:1.0.2'
    implementation 'com.github.Commit451:ForegroundViews:2.4.0'
    implementation 'com.github.Commit451:MorphTransitions:2.0.0'
    implementation "com.github.Commit451.Alakazam:alakazam:$alakazamVersion"
    implementation "com.github.Commit451.Alakazam:alakazam-kotlin:$alakazamVersion"
    implementation 'com.github.Commit451:Lift:1.2.0'
    implementation 'com.github.Commit451:okyo:2.0.0'
    implementation 'com.github.Commit451:OkioProGuardRules:1.11.0.0'
    implementation 'com.github.Commit451:RetrofitProguardRules:2.2.0.0'
    implementation 'com.github.Commit451:LoganSquareProGuardRules:1.3.7.0'
    implementation 'com.github.Commit451:EventBusProGuardRules:3.0.0.0'
    implementation "com.github.Commit451.Addendum:addendum:$addendumVersion"
    implementation "com.github.Commit451.Addendum:addendum-parceler:$addendumVersion"

    implementation 'com.github.chrisbanes:PhotoView:2.1.3'

    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.1'

    implementation 'com.github.Jawnnypoo:PhysicsLayout:2.1.0'

    implementation 'com.alexgwyn.recyclerviewsquire:recyclerviewsquire:0.0.6'

    implementation 'com.github.ivbaranov:materiallettericon:0.2.2'

    implementation 'com.github.alorma:diff-textview:1.3.0'

    implementation 'com.wdullaer:materialdatetimepicker:3.2.2'

    implementation 'com.github.novoda:simple-chrome-custom-tabs:0.1.5'

    implementation "com.afollestad.material-dialogs:core:$materialDialogsVersion"
    implementation "com.afollestad.material-dialogs:commons:$materialDialogsVersion"

    implementation 'de.hdodenhof:circleimageview:2.1.0'

    implementation('com.vdurmont:emoji-java:3.2.0') {
        exclude group: 'org.json', module: 'json'
    }

    implementation 'com.github.jkwiecien:EasyImage:2.0.2'

    implementation 'com.atlassian.commonmark:commonmark:0.9.0'

    normalImplementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    testImplementation 'junit:junit:4.12'
    testImplementation('org.threeten:threetenbp:1.3.6') {
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }

    androidTestImplementation ('com.android.support.test:runner:1.0.0') {
        exclude group:'com.android.support', module:'support-annotations'
    }
}
